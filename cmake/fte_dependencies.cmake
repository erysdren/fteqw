
include(FetchContent)

find_package(Math)

if(FTE_VENDOR_DEPENDENCIES)
	FetchContent_Declare(ZLIB
		URL "https://zlib.net/zlib-1.3.2.tar.gz"
		URL_HASH MD5=a1e6c958597af3c67d162995a342138a
		EXCLUDE_FROM_ALL
	)
	FetchContent_MakeAvailable(ZLIB)
	list(APPEND FTE_COMMON_DEFINITIONS AVAIL_ZLIB)
else()
	set(ZLIB_USE_STATIC_LIBS ON)
	find_package(ZLIB)
	if(ZLIB_FOUND)
		add_library(zlibstatic ALIAS ZLIB::ZLIB)
		list(APPEND FTE_COMMON_DEFINITIONS AVAIL_ZLIB)
	else()
		list(APPEND FTE_COMMON_DEFINITIONS NO_ZLIB)
		message(WARNING "zlib not found")
	endif()
endif()

if(FTE_PLUGIN_BOX3D)
	if(FTE_VENDOR_DEPENDENCIES)
		FetchContent_Declare(box3d
			GIT_REPOSITORY "https://github.com/erincatto/box3d.git"
			GIT_TAG "v0.1.0"
			EXCLUDE_FROM_ALL
		)
		FetchContent_MakeAvailable(box3d)
	else()
		find_package(box3d 0.1 REQUIRED)
	endif()
endif()

if(FTE_PLUGIN_BULLET)
	if(FTE_VENDOR_DEPENDENCIES)
		FetchContent_Declare(Bullet
			URL "https://github.com/bulletphysics/bullet3/archive/refs/tags/2.89.tar.gz"
			URL_HASH MD5=d239b4800ec30513879834be6fcdc376
			EXCLUDE_FROM_ALL
		)
		set(BUILD_BULLET2_DEMOS OFF CACHE STRING "")
		set(BUILD_EXTRAS OFF CACHE STRING "")
		set(BUILD_BULLET3 OFF CACHE STRING "")
		FetchContent_MakeAvailable(Bullet)
	else()
		find_package(Bullet REQUIRED)
	endif()
endif()

if(FTE_PLUGIN_JOLT)
	FetchContent_Declare(JoltPhysics
		GIT_REPOSITORY "https://github.com/jrouwe/JoltPhysics.git"
		GIT_TAG "v5.5.0"
		SOURCE_SUBDIR "Build"
		EXCLUDE_FROM_ALL
	)
	FetchContent_MakeAvailable(JoltPhysics)
endif()

if(FTE_PLUGIN_ODE)
	FetchContent_Declare(ODE
		GIT_REPOSITORY "https://bitbucket.org/odedevs/ode.git"
		GIT_TAG "0.16.6"
		EXCLUDE_FROM_ALL
	)
	set(ODE_DOUBLE_PRECISION OFF CACHE STRING "")
	FetchContent_MakeAvailable(ODE)
	target_compile_options(ODE
		PUBLIC
			$<$<AND:$<COMPILE_LANGUAGE:CXX>,$<CXX_COMPILER_ID:GNU,Clang>>:-Wno-deprecated-enum-enum-conversion>
			$<$<AND:$<COMPILE_LANGUAGE:CXX>,$<CXX_COMPILER_ID:GNU,Clang>>:-Wno-deprecated-enum-float-conversion>
	)
endif()

if(FTE_TOOL_HEIGHTMAPCONVERTER)
	FetchContent_Declare(inih
		GIT_REPOSITORY "https://github.com/benhoyt/inih.git"
		GIT_TAG "origin/master"
		EXCLUDE_FROM_ALL
	)
	FetchContent_MakeAvailable(inih)
endif()

if(FTE_ENGINE_BOTH OR FTE_ENGINE_CLIENT)
	if(FTE_ENGINE_SDL_VERSION_MAJOR STREQUAL "1")
		if(FTE_VENDOR_DEPENDENCIES)
			message(FATAL_ERROR "Vendoring SDL 1.2 is currently unsupported")
		else()
			find_package(SDL REQUIRED)
		endif()
		set(FTE_ENGINE_USE_SDL TRUE)
	elseif(FTE_ENGINE_SDL_VERSION_MAJOR STREQUAL "2")
		if(FTE_VENDOR_DEPENDENCIES)
			FetchContent_Declare(SDL2
				GIT_REPOSITORY "https://github.com/libsdl-org/SDL.git"
				GIT_TAG "release-2.32.10"
				EXCLUDE_FROM_ALL
				FIND_PACKAGE_ARGS
			)
			FetchContent_MakeAvailable(SDL2)
		else()
			find_package(SDL2 REQUIRED)
		endif()
		set(FTE_ENGINE_USE_SDL TRUE)
	elseif(FTE_ENGINE_SDL_VERSION_MAJOR STREQUAL "3")
		if(FTE_VENDOR_DEPENDENCIES)
			FetchContent_Declare(SDL3
				GIT_REPOSITORY "https://github.com/libsdl-org/SDL.git"
				GIT_TAG "release-3.4.12"
				EXCLUDE_FROM_ALL
				FIND_PACKAGE_ARGS
			)
			FetchContent_MakeAvailable(SDL3)
		else()
			find_package(SDL3 REQUIRED)
		endif()
		set(FTE_ENGINE_USE_SDL TRUE)
	else()
		set(FTE_ENGINE_USE_SDL FALSE)
	endif()
	if(FTE_ENGINE_RENDERER STREQUAL "gl")
		find_package(OpenGL REQUIRED)
	elseif(FTE_ENGINE_RENDERER STREQUAL "vk")
		find_package(Vulkan REQUIRED)
	endif()
	if(FTE_VENDOR_DEPENDENCIES)
		FetchContent_Declare(Freetype
			URL "https://download.savannah.gnu.org/releases/freetype/freetype-2.14.3.tar.gz"
			URL_HASH MD5=c8333525a49e3caf08f427f1a4b01f35
			EXCLUDE_FROM_ALL
		)
		FetchContent_MakeAvailable(Freetype)
		list(APPEND FTE_COMMON_DEFINITIONS AVAIL_FREETYPE)
	else()
		find_package(Freetype)
		if(Freetype_FOUND)
			add_library(freetype ALIAS Freetype::Freetype)
			list(APPEND FTE_COMMON_DEFINITIONS AVAIL_FREETYPE)
		else()
			message(WARNING "Freetype not found, TTF fonts will not render")
		endif()
	endif()
	if(FTE_VENDOR_DEPENDENCIES)
		FetchContent_Declare(Ogg
			URL "https://ftp.osuosl.org/pub/xiph/releases/ogg/libogg-1.3.5.tar.gz"
			URL_HASH MD5=3267127fe8d7ba77d3e00cb9d7ad578d
			EXCLUDE_FROM_ALL
			FIND_PACKAGE_ARGS
		)
		FetchContent_MakeAvailable(Ogg)
		FetchContent_Declare(Vorbis
			URL "https://ftp.osuosl.org/pub/xiph/releases/vorbis/libvorbis-1.3.7.tar.gz"
			URL_HASH MD5=9b8034da6edc1a17d18b9bc4542015c7
			EXCLUDE_FROM_ALL
			FIND_PACKAGE_ARGS
		)
		FetchContent_MakeAvailable(Vorbis)
		list(APPEND FTE_COMMON_DEFINITIONS AVAIL_OGGVORBIS)
	else()
		find_package(Ogg)
		find_package(Vorbis)
		if(Ogg_FOUND AND Vorbis_FOUND)
			list(APPEND FTE_COMMON_DEFINITIONS AVAIL_OGGVORBIS)
		else()
			message(WARNING "Ogg/Vorbis not found")
		endif()
	endif()
	list(APPEND FTE_COMMON_DEFINITIONS AVAIL_STBI)
endif()
